// room script file

// this starts the outro of the game
function play_outro()
{
  aMusic.Stop();
  aMusic = aWin.Play(eAudioPriorityHigh, eRepeat);
  cRoger.ChangeRoom(17, -25, 128, eDirectionRight);
  cRoger.Walk(25, 128, eBlock, eAnywhere);
  cRoger.Say("Who are you?");
  player.FaceCharacter(cRoger);
  player.Say("Huh? It's you!");
  player.Say("We were supposed to meet last week.");
  player.Say("This thing said you are dead.");
  cRoger.Say("Oh... that's just a glitch in the computer system.");
  cRoger.Say("Why did you come here?");
  player.Say("We found your notes and tracked it back to this building.");
  cRoger.Say("What notes?");
  player.Say("Your journal.");
  cRoger.Say("Did you share it with anyone?");
  player.Say("Well, I cannot disclose my sources until you tell me more about all of this.");
  cRoger.Say("I'm afraid that is not possible.");
  cRoger.Say("I have to insist, you come with me.");
  player.Say("What? Here? Into the back room of an abandoned warehouse? No way!");
  Wait(60);
  cRoger.Say("Then I assume we need to apply some force.");
  Wait(80);
  player.Say("Okay. Time for me to go...");
  cRoger.Walk(160, 180, eNoBlock, eAnywhere);
  player.Walk(160, 275, eBlock, eAnywhere);
  player.ChangeRoom(16, 589, 180, eDirectionLeft);
}

function hContraption_Look()
{
  player.Say("Good god, what is that?");
}

function hComputer_Look()
{
  player.Say("It's a computer!");
}

// scan the database using the computer
function hComputer_Interact()
{
  player.FaceDirection(eDirectionUp);
  if (oDisplay.Visible)
  {
    player.Say("Ready for some investigation! Let's dive into this.");
    gTerminalDark.Visible = true;
    Wait(120);
    gTDBtn.Visible = false;
    Wait(60);
    player.Say("This is the guy I was supposed to meet!");
    player.Say("It seems he didn't survive...");
    Wait(120);
    gTDBtn.Visible = true;
    Wait(120);
    gTerminalDark.BackgroundGraphic = 255;
    gTDBtn.Visible = false;
    Wait(60);
    player.Say("Oh no...");
    Wait(60);
    player.Say("Why wouldn't she stop when I told her to!");
    Wait(60);
    player.Say("She was always worried by this project.");
    Wait(40);
    player.Say("But I assume she believed in it enough to continue");
    player.Say("until the end.");
    Wait(120);
    gTerminalDark.Visible = false;
    bOutro = true;
    play_outro();
  }
  else
  {
    player.Say("It's missing a monitor.");
  }
}

function oScrewdriver_Interact()
{
  player.Walk(oScrewdriver.X, oScrewdriver.Y, eBlock);
  player.Say("A screwdriver. I'll take it with me.");
  player.AddInventory(iScrewdriver);
  oScrewdriver.Visible = false;
  oScrewdriver.Clickable = false;
}


// go back to the previous room
function hGoBack_Interact()
{
  player.Walk(player.x, 250, eBlock, eAnywhere);
  player.ChangeRoom(16, 589, 180, eDirectionLeft);
}

// use display to use the computer
function hComputer_UseInv()
{
  player.FaceDirection(eDirectionUp);
  if (player.ActiveInventory == iDisplay)
  {
    oDisplay.Visible = true;
    player.LoseInventory(iDisplay);
    player.Say("It worked! The computer is up and running.");
  }
  else
  {
    player.Say("That won't work.");
  }
  player.ActiveInventory = null;
}

function oScrewdriver_Look()
{
  player.Walk(oScrewdriver.X, oScrewdriver.Y, eBlock);
  player.Say("A screwdriver.");
}
