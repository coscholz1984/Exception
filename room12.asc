// room script file

// move to the next section of the game
function move_to_next()
{
  gBlack.Visible = true;
  player.ChangeRoom(14, 300, 300);
}

function room_FirstLoad()
{
  aMusic.Stop();
  aMusic = aInside.Play(eAudioPriorityHigh, eRepeat);
  // Animate cradle
  oCradle.SetView(20);
  oCradle.Animate(0, 7, eRepeat, eNoBlock);
  // Animate Light
  oLight.SetView(23);
  oLight.Animate(0, 2, eRepeat, eNoBlock);
  for (int i=0; i<5;i++)
  {
    ButtonArray1[i] = 0;
  }
  // FadeIn-trick
  FadeOut(64);
  cBob.ChangeRoom(12, 232, 157);
  cBob.Animate(0, 1 , eRepeat, eNoBlock);
  gBlack.Visible = false;
  FadeIn(5);
}

// Move into the server room
function hDoor_Interact()
{
  player.Walk(hDoor.WalkToX, hDoor.WalkToY, eBlock);
  player.FaceDirection(eDirectionUp);
  Wait(10);
  oDoor.Visible = true;
  Wait(10);
  player.ChangeRoom(13, 142, 142, eDirectionDown);
  oDoor.Visible = false;
}

// Here we recieve the mail from Fiona
// Action depends on state of solving the puzzle
function hTerminal_Interact()
{
  player.Walk(hTerminal.WalkToX, hTerminal.WalkToY, eBlock);
  player.FaceDirection(eDirectionRight);
  if (bMail == false)
  {
  gTextbox2.Text = "You have 1 new mail.";
  gTextbox3.Text = "Loading mails into memory...";
  gTerminal2.Visible = true;
  Wait(120);
  gTextbox2.Text = "Mail from @Fiona";
  gTextbox3.Text = "Hi Henk.\n I've got something really interesting. The scan is attached. Could you have a look for me, check if we have any matching dates?\n - Fiona";
  gTextbox3.Visible = true;
  Wait(360);
  gTextbox3.Visible = false;
  gTextbox2.Text = "Mail from @Fiona - Attachments";
  gBtnBook.Visible = true;
  Wait(80);
  player.Say("Interesting, I should run OCR on the scans and check the dates.");
  }
  bMail = true;
  gTerminal2.Visible = true;
  gTextbox2.Text = "Starting OCR server.";
  Wait(40);
  gTextbox2.Text = "Starting OCR server..";
  Wait(40);
  gTextbox2.Text = "Starting OCR server...";
  Wait(40);
  gTextbox2.Text = "Starting OCR server....";
  Wait(40);
  if (bServer == false)
  {
    gTextbox2.Text = "Error - no response.";
    Wait(60);
    player.Say("Damn, something is wrong with the server.");
    player.Say("I need to fix it first.");
    Wait(40);
  } 
  else
  {
    if (bForce == false)
    {
      gTextbox2.Text = "Server started.";
      Wait(40);
      gTextbox2.Text = "Waiting for process.";
      Wait(40);
      gTextbox2.Text = "Waiting for process..";
      Wait(40);
      gTextbox2.Text = "Waiting for process...";
      Wait(40);
      gTextbox2.Text = "Unknown error.";
      Wait(40);
      player.Say("Damn thing, something is still wrong.");
      Wait(30);
    }
    else
    {
      gTextbox2.Text = "Server started.";
      Wait(40);
      gTextbox2.Text = "Waiting for process.";
      Wait(40);
      gTextbox2.Text = "OCR finished. Extracting dates.";
      Wait(40);
      gBtnBook.Visible = false;
      gList1.AddItem("01/01/97 Atlstr. 37, LN");
      gList1.AddItem("01/25/97 Graystr. 5, HU");
      gList1.AddItem("01/29/97 Canlrd. 23, KO");
      gList1.AddItem("03/25/97 Graystr. 5, HU");
      gList1.AddItem("05/25/97 Graystr. 5, HU");
      gList1.AddItem("07/25/97 Graystr. 5, HU");
      gList2.AddItem("01/03/97");
      gList2.AddItem("01/26/97");
      gList2.AddItem("03/26/97");
      gList2.AddItem("05/26/97");
      gList2.AddItem("07/26/97");
      gList1.SelectedIndex = -1;
      gList2.SelectedIndex = -1;
      gTextbox3.Text = "Data traffic peaks  |  OCR dates";
      gTextbox3.Visible = true;
      gList1.Visible = true;
      gList2.Visible = true;
      Wait(100);
      player.Say("Look at that. Several entries, always after large data transfers from Graystreet.");
      player.Say("Fiona could check out that address.");
      Wait(30);
      move_to_next();
    }
  }
  gTerminal2.Visible = false;
}

function hTerminal_Look()
{
  cHenk.Say("A terminal to log into our server.");
}

// Look at cradle and display an achievement
function hCradle_Look()
{
  player.FaceLocation(204, 108, eNoBlock);
  player.Say("It's our perpetual Newton's cradle. Beth refuses to explain the trick to me, but I've never seen it stop.");
  display_achievement(3, "Achievement: History repeating?");
}
