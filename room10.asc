// room script file

bool bScanned = false;

// Excuted for any click on the scanner
function hScanner_AnyClick()
{
  cFiona.Walk(hScanner.WalkToX, hScanner.WalkToY, eBlock);
  cFiona.FaceDirection(eDirectionRight);
  if (((cFiona.ActiveInventory == iBook) || (cFiona.ActiveInventory == null)) && (bScanned == false))
  {
    cFiona.Say("Nice. A document scanner for books.");
    cFiona.Say("I'll scan in the pages.");
    Wait(40);
    cFiona.FaceDirection(eDirectionDown);
    cFiona.Say("Okay, now I need to use the computer to send the scans.");
    cFiona.ActiveInventory = null;
    bScanned = true;
  }
  else
  {
    if (bScanned)
    {
      cFiona.Say("I already did that.");
    }
    else
    {
      cFiona.Say("That doesn't work.");
    }
  }
}

// Executed for interaction with the computer
function hComputers_Interact()
{
  if (bScanned)
  {
    cFiona.Walk(hComputers.WalkToX, hComputers.WalkToY, eBlock);
    cFiona.FaceDirection(eDirectionUp);
    gTerminal.Visible = true;
  }
  else
  {
    cFiona.Say("No need to use that yet.");
  }
}

// Dummy function in case we want a different music to play in this room
function room_Load()
{
  // Only update music, if not already playing aInside
  //aMusic.Stop();
  //aMusic = aInside.Play(eAudioPriorityHigh, eRepeat);
}

// Fade-in trick and start room
function room_AfterFadeIn()
{
  // FadeIn-trick
  FadeOut(64);
  gBlack.Visible = false;
  FadeIn(2);
  cFiona.Walk(133, 160, eBlock, eAnywhere);
  cFiona.FaceDirection(eDirectionDown);
}

// Small monologe when clicking on books
function hBooks_Interact()
{
  cFiona.FaceDirection(eDirectionDown);
  cFiona.Say("Wow. Hundreds of books!");
  cFiona.Say("It's like I'm in a library...");
  cFiona.Say("Unfortunatly, I don't have time to check out any of them.");
}
