// room script file

int ibell = 0;

// First load and init episode
function room_FirstLoad()
{
  // FadeIn-trick
  cFiona.AddInventory(iBlacklight);
  oClouds.SetView(9);
  oClouds.Animate(0, 80, eRepeat, eNoBlock);
  aMusic.Stop();
  aMusic = aInside.Play(eAudioPriorityHigh, eRepeat);
  FadeOut(64);
  gBlack.Visible = false;
  FadeIn(2);  
  cGuy.ChangeRoom(8, 287, 136);
  cWhite.ChangeRoom(8, 400, 400); //using him to display the bell sound
  cFiona.Walk(166, 153, eBlock, eAnywhere);
  cFiona.FaceDirection(eDirectionDown, eBlock);
  Wait(20);
  cFiona.Say("Okay, this is the last place that I'm going to check.");
  cFiona.Say("If he wasn't here, I'll forget the whole story.");
}

// The bell is used to summon Roy and make him go to the hotel room
// Multiple clicks on the bell are counted to make Roy angry
function hBell_Interact()
{
  if (ibell < 1)
  {
    cFiona.Walk(hBell.WalkToX, hBell.WalkToY, eBlock);
    cFiona.FaceDirection(eDirectionUp);
    cFiona.Say("Nobody home? I guess I'll try to ring the bell.");
    oDoorOpen.Visible = true;
    cGuy.Walk(256, 133, eBlock, eAnywhere);
    cGuy.Say("Don't touch that bell! I hate the sound.");
    cGuy.Walk(165, 133, eBlock, eAnywhere);
    cGuy.FaceDirection(eDirectionDown);
    cFiona.FaceCharacter(cGuy, eBlock);
    cGuy.Say("So?");
    dMotel.Start();
  ibell++;
  }
  else
  {
    if (ibell < 2)
    {
      cFiona.Walk(hBell.WalkToX, hBell.WalkToY, eBlock);
      cFiona.FaceCharacter(cGuy, eBlock);
      cWhite.SayAt(135, 108, 40, "Ding!");
      cWhite.SayAt(144, 104, 40, "Ding!");
      cWhite.SayAt(156, 106, 40, "Ding!");
      cGuy.Say("Stop it!");
      ibell++;
    }
    else
    {
      if (ibell < 3)
      {
        cFiona.Walk(hBell.WalkToX, hBell.WalkToY, eBlock);
        cFiona.FaceCharacter(cGuy, eBlock);
        cWhite.SayAt(135, 108, 60, "Ding! Ding!");
        cWhite.SayAt(144, 104, 60, "Ding! Ding!");
        cWhite.SayAt(156, 106, 60, "Ding! Ding! Ding!");
        cGuy.Say("If you do that one more time...!");
        ibell++;
      }
      else
      {
        cFiona.Walk(hBell.WalkToX, hBell.WalkToY, eBlock);
        cFiona.FaceCharacter(cGuy, eBlock);
        cWhite.SayAt(135, 108, 60, "Dingelingegegeling!");
        cWhite.SayAt(144, 104, 60, "Dingelingegegegegeling!");
        cWhite.SayAt(156, 106, 60, "Dingelingegeggegegegeling!");
        cGuy.Say("Will you stop this, if I let you into the room?");
        cFiona.Say("I think that is a reasonable proposal.");
        cGuy.Say("Okay, fine, follow me.");
        Wait(15);
        cGuy.Walk(287, 136, eNoBlock, eAnywhere);
        cFiona.Walk(262, 140, eBlock, eWalkableAreas);
        cFiona.ChangeRoom(9, 264, 144, eDirectionLeft);
      }
    }
  }
}

// Interact with or look at the brochures
function hBrochures_Interact()
{
  cFiona.Walk(hBrochures.WalkToX, hBrochures.WalkToY, eBlock);
  cFiona.FaceDirection(eDirectionUp);
  cFiona.Say("Those are some of the ugliest brochures that I have ever seen.");
  display_achievement(2, "Achievement: Read! Read! Read!");
}

// Check out the keyrack (no keys, because Roy keeps all in his pocket)
function hKeyrack_Interact()
{
  cFiona.Walk(hKeyrack.WalkToX, hKeyrack.WalkToY, eBlock);
  cFiona.FaceDirection(eDirectionUp);
  cFiona.Say("There are no keys on the key rack. Weird.");
}

// Interact with door (pointless)
function hDoor_Interact()
{
  if (!oDoorOpen.Visible)
  {
    cFiona.Walk(hDoor.WalkToX, hDoor.WalkToY, eBlock, eWalkableAreas);
    cFiona.FaceDirection(eDirectionRight);
    cFiona.Say("Hello? Anyone home?");
    Wait(60);
    cFiona.Say("Hm. No reply.");
  }
  else
  {
    cFiona.Say("I need to convince the guy to show me the room first.");
  }
}

// Look at the door (pointless)
function hDoor_Look()
{
  cFiona.FaceLocation(hDoor.WalkToX, hDoor.WalkToY);
  cFiona.Say("A door.");
}
