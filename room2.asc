// room script file

// Progress to next room
function MoveToNextRoom()
{
  cRoger.Walk(hDoor.WalkToX, hDoor.WalkToY, eBlock);
  cRoger.FaceLocation(hDoor.WalkToX, hDoor.WalkToY, eBlock);
  cRoger.ChangeRoom(3, 266, 143);
  cRoger.FaceDirection(eDirectionLeft);
}

// Move into hotel room
function hDoor_UseInv()
{
  if (cRoger.ActiveInventory == iKey)
  {
    MoveToNextRoom();
  }
  cRoger.ActiveInventory = null;
}

// Interact with car
function hCar_AnyClick()
{
  if (cRoger.ActiveInventory == null) 
  {
    cRoger.FaceLocation(hCar.WalkToX,  hCar.WalkToY, eBlock);
    cRoger.Say("My car. A pretty robust vehicle.");
  }
  else 
  {
    if (cRoger.ActiveInventory == iKey) 
    {
      cRoger.Say("These are the keys to the hotel room, not my car.");
    }
    else 
    {
      cRoger.Say("I cannot use these things together.");
    }
  }
}

// Look at the door
function hDoor_Look()
{
  cRoger.FaceLocation(hDoor.WalkToX, hDoor.WalkToY, eBlock);
  cRoger.Say("The door to my room.");
}

// Open the door
function hDoor_Interact()
{
  if (cRoger.HasInventory(iKey))
  {
    MoveToNextRoom();
  }
}

// Look into room windows
function hWindows_AnyClick()
{
  if (cRoger.ActiveInventory == null) 
  {
    cRoger.Walk(182, 152, eBlock);
    cRoger.FaceLocation(180, 0, eBlock);
    cRoger.Say("Can't see anything on the inside. It's too dark.");
  } 
  else {
    cRoger.Say("I cannot use these things together.");
  }
}

// Switch music, play dialog
function room_FirstLoad()
{
  aMusic.Stop();
  aMusic = aOutside.Play(eAudioPriorityHigh, eRepeat);
  oSign.SetView(21, 0);
  oSign.Animate(0, 4, eRepeat, eNoBlock);
  FadeOut(64);
  gBlack.Visible = false;
  FadeIn(1);
  cRoger.Walk(150, 180, eBlock, eAnywhere);
  cRoger.FaceDirection(eDirectionDown, eBlock);
  cGuy.Walk(180, 185, eBlock, eAnywhere);
  cGuy.FaceDirection(eDirectionLeft, eBlock);
  cRoger.FaceDirection(eDirectionRight, eBlock);
  cRoger.Say("....");
  cGuy.Say("Okay, here is the key for room 11.", eBlock);
  cGuy.Say("I just couldn't find it.", eBlock);
  cGuy.Say("Guess what? It was still in my pocket.", eBlock);
  cRoger.AddInventory(iKey);
  cGuy.Walk(170, 200, eBlock, eAnywhere);
  cGuy.FaceDirection(eDirectionUp);
  Wait(15);
  cRoger.FaceDirection(eDirectionDown);
  Wait(40);
  cGuy.Say("Oh, if there's a problem...");
  cGuy.Say("Do me a favor, swallow it down.");
  cGuy.Say("I'll give you a refund tomorrow if you don't trash the place.");
  cGuy.Walk(160, 280, eBlock, eAnywhere);
  cRoger.Say("Sounds promising.");
}

// Look at the newspaper (easter-egg)
function hNewspaper_AnyClick()
{
  cRoger.Walk(hNewspaper.WalkToX, hNewspaper.WalkToY, eBlock);
  cRoger.Say("That's a newspaper from last week.");
  gNewspaper.Visible = true;
}

function hDoor2_Look()
{
  player.Say("It's the door to room 10.");
}

function hDoor2_UseInv()
{
  if (player.ActiveInventory == iKey)
  {
    player.Say("That's not my room.");
  }
  else
  {
    player.Say("That won't do anything.");
  }
}

function hWindows2_Interact()
{
  cRoger.Walk(75, 161, eBlock, eWalkableAreas);
  cRoger.FaceDirection(eDirectionUp);
  cRoger.Say("It's very dark. I don't think anyone is staying there.");
  cRoger.Say("But it's not my room anyway.");
}

function hWindows2_Look()
{
  cRoger.Say("A window.");
}
