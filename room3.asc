// room script file

// Use the TV (switch on with remote)
function hTV_UseInv()
{
  cRoger.Walk(hTV.WalkToX, hTV.WalkToY,  eBlock);
  cRoger.FaceLocation(hTV.WalkToX, hTV.WalkToY-60, eBlock);
  if (cRoger.ActiveInventory == iRemote)
  {
    if (bBatteries)
    {
      cRoger.ChangeRoom(4, 160, 100);
    } 
    else
    {
      cRoger.Say("Hm. Nothing. The batteries are probably empty.");
    }
  } 
  else
  {
    cRoger.Say("That doesn't work.");
  }
  cRoger.ActiveInventory = null;
}

// Pick up TV remote control
function iRemoteControl_Interact()
{
  cRoger.Walk(iRemoteControl.X, iRemoteControl.Y+32,  eBlock);
  cRoger.Say("A remote control for the TV. I'll take it.");
  cRoger.AddInventory(iRemote);
  iRemoteControl.Visible = false;
  iRemoteControl.Clickable = false;
}

// Lock at remote control
function iRemoteControl_Look()
{
  cRoger.Say("A remote control for the TV. Could come in handy.");
}

// Switch on TV if possible
function hTV_Interact()
{
  if (bTV)
  {
    cRoger.Say("I'm not going to turn that thing on again...");
  }
  else {
    cRoger.Walk(hTV.WalkToX, hTV.WalkToY,  eBlock);
    cRoger.FaceLocation(hTV.WalkToX, hTV.WalkToY-60, eBlock);
    if (cRoger.HasInventory(iRemote)) 
    {
      if (bBatteries)
      {
        cRoger.ChangeRoom(4, 160, 100);
      } 
      else
      {
        cRoger.Say("It doesn't turn on. The remote control's batteries are probably empty.");
      }
    }
    else 
    {
      cRoger.Say("Hm. I think I need a remote control to switch it on.");
    }
  }
}

// Look at the TV
function hTV_Look()
{
  cRoger.Say("A TV.");
}

// Go to bed if possible
function hBed_Interact()
{
  if (bTV)
  {
    if (bHidden) 
    {
      cRoger.Say("Okay, time to sleep.");
      cRoger.Walk(hBed.WalkToX, hBed.WalkToY, eBlock);
      FadeOut(5);
      cRoger.ChangeRoom(6, 30, 160);
      gBlack.Visible = true;
    } 
    else 
    {
      cRoger.Say("I really need some sleep.");
      cRoger.Say("But first, I should hide my journal somewhere.");
      cRoger.Say("They don't know that I still have it.");
    }
  } 
  else 
  {
    cRoger.Say("I don't want to sleep yet.");
    cRoger.Say("Just need a bit of distraction first.");
  }
}

// Look at bed
function hBed_Look()
{
  cRoger.Say("A bed.");
}

// Look out of window
function hWindow_Look()
{
  cRoger.Say("A view of the city.");
}

// Look at alarm clock, hint to batteries
function hAlarm_Look()
{
  cRoger.FaceLocation(hAlarm.WalkToX, hAlarm.WalkToY, eBlock);
  cRoger.Say("A battery powered alarm clock.");
}

// Use batteries from alarm for remote ctrl
function hAlarm_UseInv()
{
  cRoger.FaceLocation(hAlarm.WalkToX, hAlarm.WalkToY, eBlock);
  if (cRoger.ActiveInventory == iRemote)
  {
    cRoger.Walk(hAlarm.WalkToX, hAlarm.WalkToY, eBlock);
    cRoger.Say("Nice. I can use the batteries for the remote control.");
    oAlarm.Graphic = 147;
    bBatteries = true;
    cRoger.ActiveInventory = null;
  } 
  else
  {
    cRoger.Say("That doesn't work.");
  }
}

// Look at the door
function hDoor_Look()
{
  cRoger.FaceLocation(hDoor.WalkToX, hDoor.WalkToY, eBlock);
  cRoger.Say("The door.");
}

// Try to leave
function hDoor_Interact()
{
  cRoger.Say("Not going to leave today anymore.");
}

// Switch music and play dialog
function room_FirstLoad()
{
  aMusic.Stop();
  aMusic = aInside.Play(eAudioPriorityHigh, eRepeat);
  Wait(1);
  cRoger.Say("Hm. Not as bad as I thought.");
  cRoger.Say("...");
  cRoger.Say("At least there is a TV.");
  cRoger.Say("I think I'll watch a bit before going to bed.");
}

// Hide Journal
function hBoard_UseInv()
{
  cRoger.Walk(hBoard.WalkToX, hBoard.WalkToY, eBlock);
  if (cRoger.ActiveInventory == iBook) 
  {
    cRoger.Say("I'll hide it in there.");
    cRoger.Say("Just in case they catch me here.");
    cRoger.LoseInventory(iBook);
    bHidden = true;
  } 
  else 
  {
    cRoger.Say("No, I don't need to put this here.");
  }
}

// Interact with board
function hBoard_Interact()
{
  cRoger.Walk(hBoard.WalkToX, hBoard.WalkToY, eBlock);
  if (bHidden) 
  {
    cRoger.Say("I've put my journal inside to hide it.");
  } 
  else 
  {
    cRoger.Say("A cupboard with a TV on top. It's empty.");
  }
}

// Look at board
function hBoard_Look()
{
  cRoger.FaceLocation(hBoard.WalkToX,  hBoard.WalkToY-50, eBlock);
  cRoger.Say("A cupboard with a TV on top.");
}

// Load room at the end
function room_Load()
{
  oStrings.SetView(10);
  oStrings.Animate(0, 10, eRepeat, eNoBlock);
  if (bEndAct)
  {
    cRoger.x = 150;
    cRoger.y = 150;
    cRoger.Transparency = 0;
    cRoger.SetAsPlayer();
    cRoger.FaceDirection(eDirectionRight);
  }
}

// Play outro
function room_AfterFadeIn()
{
  if (bEndAct)
  {
    FadeOut(64);
    gBlack.Visible = false;
    FadeIn(1);
    cRoger.Say("Oh, my head...");
    cRoger.Say("Ah, I ....");
    cRoger.Say("I understand.");
    cRoger.Say("I need to return immediatly.");
    cRoger.SetAsPlayer();
    cRoger.Walk(hDoor.WalkToX, hDoor.WalkToY, eBlock);
    Wait(20);
    oOpendoor.Visible = true;
    cRoger.Walk(317, hDoor.WalkToY, eBlock, eAnywhere);
    cWhite.ChangeRoom(7, 160, 100);
    cRoger.ChangeRoom(7, 160, 100);
    FadeOut(2);
  }
}
