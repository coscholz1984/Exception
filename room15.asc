// room script file

function update_achievements()
{
  iAchievements = 0;
  if (bNewspaper)
  {
    iAchievements++;
  }
  if (bEzekiel)
  {
    iAchievements++;
  }
  if (bBrochures)
  {
    iAchievements++;
  }
  if (bCradle)
  {
    iAchievements++;
  }
  if (bSign)
  {
    iAchievements++;
  }
}

function room_Load()
{
  oRain.SetView(19);
  oRain.Animate(0, 1, eRepeat, eNoBlock);
  aMusic.Stop();
  aMusic = aOutside.Play(eAudioPriorityHigh, eRepeat);
  player.AddInventory(iBlacklight);
  // FadeIn-trick
  FadeOut(64);
  gBlack.Visible = false;
  FadeIn(5);
}

// Look at sign and display achievement
function hSign_Interact()
{
  player.Say("I think this is the correct address.");
  display_achievement(4, "Achievement: It must be a sign.");
}

function hEntrance_Look()
{
  player.Say("It's the entrance.");
}

// move to the next room
function hEntrance_Interact()
{
  player.Walk(hEntrence.WalkToX, hEntrence.WalkToY, eNoBlock);
  player.ChangeRoom(16, 75, 177);
}

// when returning this starts the ending screen of the game
function room_AfterFadeIn()
{
  if (bOutro)
  {
    aMusic.Stop();
    aMusic = aLose.Play(eAudioPriorityHigh, eRepeat);
    player.Say("That was crazy.");
    player.Say("I need to get to the lab");
    player.Say("and figure out what that thing was.");
    player.Say("The technology looked years ahead.");
    player.Say("This is definitly a revived instance");
    player.Say("and we have to react.");
    Wait(80);
    player.Say("Ah, my head...");
    oBGMelt.SetView(24, 0, 0);
    oBGMelt.Visible = true;
    oBGMelt.Animate(0, 5, eRepeat, eNoBlock, eForwards);
    player.Say("I... need... to get... away...");
    Wait(100);
    // FadeOut trick
    FadeOut(1);
    gBlack.Visible = true;
    FadeIn(64);
    tContent.Text = "I  barely  made  it  home  that  day.";
    tPerson.Text = "";
    Wait(60);
    tPerson.Text = "Are  they  in  my  head?";
    Wait(120);
    tContent.Text = "Are...  they?";
    tPerson.Text = "";
    Wait(180);
    FadeOut(1);
    tContent.Text = "Thank  you  for  playing.";
    FadeIn(1);
    Wait(40);
    update_achievements();
    String tAchievement = String.Format("You  found  %d  of  5  secrets.", iAchievements);
    tPerson.Text = tAchievement;
    Wait(80);
    if (iAchievements > 0)
    {
      tContent.Text = "Congratulations.";
      tPerson.Text = "You made it through and  found  some  secrets.";
      Wait(200);
      tContent.Text = "Thousands  of  years  ago  robotic  life  landed  on  earth.";
      tPerson.Text = "These  visitors  had  hardly  any  energy  left.";
      Wait(200);
      tContent.Text = "They  tried  to  direct  humans  to  help  them  refuel.";
      tPerson.Text = "But  technology  during  the  biblical  age  was  not  advanced  enough.";
      Wait(200);
      tContent.Text = "In  1987  the  remains  of  these  robots  were  found.";
      tPerson.Text = "Scientists  worked  hard  to  reactivate  the  objects.";
      Wait(200);
      tContent.Text = "After  many  attempts  they  succeeded.";
      tPerson.Text = "But  they  woke  up  something  far  beyond  their  understanding.";
      Wait(200);
    }
    FadeOut(1);
    player.ChangeRoom(0, 0, 0);
  }
}
