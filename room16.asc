// room script file

// go back through entrance
function hEntrence_Interact()
{
  player.Walk(hEntrence.WalkToX, hEntrence.WalkToY, eBlock);
  player.FaceDirection(eDirectionLeft);
  player.ChangeRoom(15, 163, 125);
}

// Go to the last chamber
function hExit_Interact()
{
  player.ChangeRoom(17, 163, 190);
}

// This is to eventually pick up goo from the drain
function hDrain_UseInv()
{
  player.FaceDirection(eDirectionRight);
  if ((player.ActiveInventory != iBlacklight) & (player.ActiveInventory != iRod))
  {
    if (player.ActiveInventory == iRodSlime)
    {
      player.Say("I already picked up some slime.");
    }
    else
    {
      player.Say("That won't do anything.");
    }
  }
  else
  {
  if (player.ActiveInventory == iBlacklight)
  {
    oSlime.Visible = true;
    player.Say("Ah, I can see some kind of goo dripping into the drain now.");
  }
  if (player.ActiveInventory == iRod)
  {
    if (oSlime.Visible)
    {
      player.Say("I picked up some goo with the rod.");
      player.LoseInventory(iRod);
      player.AddInventory(iRodSlime);
    }
    else
    {
      player.Say("I can't see what's down there properly.");
    }
  }
  }
  player.ActiveInventory = null;
}

// here you get the rod
function hCrate2_Interact()
{
  player.FaceDirection(eDirectionUp);
  if (player.HasInventory(iRod) | player.HasInventory(iRodSlime))
  {
    player.Say("It's empty.");
  }
  else
  {
    player.Say("There is a rod inside. I'll take it.");
    player.AddInventory(iRod);
  }
}

// empty crate
function hCrate1_Interact()
{
  player.FaceDirection(eDirectionUp);
  player.Say("It's empty.");
}

// take the board
function oBoard_Interact()
{
  player.Walk(oBoard.X, oBoard.Y, eBlock, eWalkableAreas);
  player.Say("A board. Could be useful.");
  oBoard.Visible = false;
  oBoard.Clickable = false;
  player.AddInventory(iBoard);
}

// just to check out the display, it cannot be used it
function oDisplay_Interact()
{
  player.FaceObject(oDisplay);
  if (player.x < 460)
  {
    player.Say("I cannot do anything with it from here.");
  }
  else
  {
    player.Walk(oDisplay.X,  oDisplay.Y, eBlock, eWalkableAreas);
    player.Say("A display. It's screwed to the wall.");
  }
}

// get the board over the crack to step over
function hCrack_UseInv()
{
  player.FaceLocation(428, 162, eBlock);
  if (player.ActiveInventory == iBoard)
  {
    player.Say("I can use the board to cross the crack.");
    oBoard2.Visible = true;
    oBoard2.Clickable = true;
    player.LoseInventory(iBoard);
  }
  else
  {
    player.Say("That won't work.");
  }
  player.ActiveInventory = null;
}

// Cross the crack using the board
function oBoard2_Interact()
{
  if (player.x < 460)
  {
    player.Say("I hope it is save enough.");
    if (bBoardGoo)
    {
      player.Walk(395, 168, eBlock, eAnywhere);
      player.Walk(482, 168, eBlock, eAnywhere);
      player.Say("Can't believe I made it across...");
    }
    else
    {
      player.Walk(402, 168, eBlock, eAnywhere);
      player.SayAt(150, 168, 100, "Queek Queek.");
      Wait(20);
      player.Walk(391, 168, eBlock, eAnywhere);
      player.Say("No, it's too wobbly.");
      player.Say("Maybe I can stabilize it somehow.");
    }
  }
  else
  {
    player.Say("I hope it is save enough.");
    player.Walk(464, 168, eBlock, eAnywhere);
    player.Walk(391, 168, eBlock, eAnywhere);
    player.Say("Can't believe I made it across... again.");
  }
}

function oBoard_Look()
{
  player.FaceObject(oBoard);
  player.Say("A board.");
}

// Use goo to stability board
function oBoard2_UseInv()
{
  player.FaceObject(oBoard2);
  if (bBoardGoo)
  {
    player.Say("I'll just leave it in it's fragile state and hope that I'll get over it alive.");
  }
  else
  {
    player.Walk(391, 168, eBlock, eAnywhere);
    player.FaceObject(oBoard2);
    if (player.ActiveInventory == iRodSlime)
    {
      bBoardGoo = true;
      player.Say("I used the goo to fix the board to the floor as good as possible.");
    }
    else
    {
      player.Say("That won't work.");
    }
  }
  player.ActiveInventory = null;
}

function hCrack_Interact()
{
  player.Say("Ouch. A big crack in the floor.");
  player.Say("I need to find a save way to get across.");
}

function oDisplay_Look()
{
  player.FaceObject(oDisplay);
  player.Say("A display.");
}

// Unscrew the display from the wall
function oDisplay_UseInv()
{
  player.FaceObject(oDisplay);
  if (player.x < 460)
  {
    player.Say("I cannot do anything with it from here.");
  }
  else
  {
    player.Walk(oDisplay.X,  oDisplay.Y, eBlock, eWalkableAreas);
    player.FaceObject(oDisplay);
    if (player.ActiveInventory == iScrewdriver)
    {
      player.Say("I'll unscrew it from the wall.");
      Wait(40);
      oDisplay.Visible = false;
      oDisplay.Clickable = false;
      player.AddInventory(iDisplay);
      player.Say("Nice. I'll take it with me.");
    }
    else
    {
      player.Say("That won't work.");
    }
  }
  player.ActiveInventory = null;
}

// Part of the outro in this room
function room_AfterFadeIn()
{
  if (bOutro)
  {
    player.Walk(482, 166, eBlock, eAnywhere);
    cRoger.ChangeRoom(16, 582, 180, eDirectionLeft);
    cRoger.Walk(503, 166, eNoBlock, eAnywhere);
    player.Walk(382, 166, eBlock, eAnywhere);
    player.FaceDirection(eDirectionRight);    
    oBoard2.Visible = false;
    cRoger.Walk(482, 166, eBlock, eAnywhere);
    player.Say("Leave me alone you creep!");
    cRoger.Say("Even if you leave now ...");
    cRoger.Say("you are now connected to us.");
    player.Say("Oh really? I wonder what the police has to say about that.");
    Wait(40);
    cRoger.ChangeRoom(17, 0, 0);
    Wait(2);
    cWhite.ChangeRoom(16, 482, 166, eDirectionLeft);
    cWhite.Transparency = 0; // make sure it is not transparent from previous events
    Wait(4);
    cWhite.ChangeRoom(17, 0, 0);
    Wait(4);
    cRoger.ChangeRoom(16, 482, 166, eDirectionLeft);
    Wait(4);
    cRoger.ChangeRoom(17, 0, 0);
    Wait(4);
    cWhite.ChangeRoom(16, 482, 166, eDirectionLeft);
    Wait(60);
    cWhite.Say("There is nothing in this");
    cWhite.Say("empty warehouse...");
    Wait(60);
    player.Say("Who ... what are you?");
    cWhite.Say("Don't you know that?");
    cWhite.Say("You are not ready yet. But there is nothing to be afraid of.");
    oMarkings.Transparency = 100;
    oMarkings.Visible = true;
    int trans = oMarkings.Transparency;
    while (trans > 0) {
      trans--;
      oMarkings.Transparency = trans;
      Wait(1);
    }
    player.Say("Erm, okay. I'm out.");
    player.Walk(53, 181, eBlock, eAnywhere);
    player.ChangeRoom(15, 160, 175, eDirectionDown);
  }
}

function room_FirstLoad()
{
  aMusic.Stop();
  aMusic = aInside.Play(eAudioPriorityHigh, eRepeat);
}
