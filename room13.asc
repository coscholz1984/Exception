// room script file


// move back into the office
function hDoor_Interact()
{
  player.Walk(hDoor.WalkToX, hDoor.WalkToY, eBlock);
  player.FaceDirection(eDirectionUp);
  Wait(10);
  oDoor.Visible = true;
  Wait(10);
  player.ChangeRoom(12, 109, 145, eDirectionDown);
  oDoor.Visible = false;
}

function room_FirstLoad()
{
  oServer.SetView(17);
  oServer.Animate(0, 10, eRepeat, eNoBlock, eForwards);
}

// the mop is needed in the end to "fix" the server
function hMop_Interact()
{
  player.Walk(hMop.WalkToX, hMop.WalkToY, eBlock);
  player.FaceObject(oMopp);
  if (bServer == false)
  {
    player.Say("I don't want to carry that around without a good reason.");
  }
  else
  {
    player.Say("Hm, I assume it is time for more drastic methods.");
    player.Say("I'll take it with me.");
    oMopp.Visible = false;
    hMop.Enabled = false;
    player.AddInventory(iMop);
  }
}

function hMop_Look()
{
  player.Say("A mop. Used for cleaning, I assume.");
}

// push the top button
function hButton1_Interact()
{
  player.Walk(hServer.WalkToX-10, hServer.WalkToY, eBlock);
  player.FaceDirection(eDirectionRight);
  for (int i = 1; i<5; i++)
  {
    ButtonArray1[i-1] = ButtonArray1[i];
  }
  ButtonArray1[4] = 1;
  player.Say("I've pushed the top button.");
  if ((ButtonArray1[0] == 1) & (ButtonArray1[1] == 3) & (ButtonArray1[2] == 2) & (ButtonArray1[3] == 2) & (ButtonArray1[4] == 1))
  {
    bServer = true;
    player.Say("I think that was the correct sequence.");
    player.Say("But it doesn't look like it did anything.");
  }
}

// push the middle button
function hButton2_Interact()
{
  player.Walk(hServer.WalkToX-10, hServer.WalkToY, eBlock);
  player.FaceDirection(eDirectionRight);
  for (int i = 1; i<5; i++)
  {
    ButtonArray1[i-1] = ButtonArray1[i];
  }
  ButtonArray1[4] = 2;
  player.Say("I've pushed the middle button.");
}

// push the lower button
function hButton3_Interact()
{
  player.Walk(hServer.WalkToX-10, hServer.WalkToY, eBlock);
  player.FaceDirection(eDirectionRight);
  for (int i = 1; i<5; i++)
  {
    ButtonArray1[i-1] = ButtonArray1[i];
  }
  ButtonArray1[4] = 3;
  player.Say("I've pushed the lower button.");
}

function hServer_Interact()
{
  player.Say("That's our server. It runs all essential services. For instance OCR.");
  player.Say("Sometimes it's a bit unstable. Beth knows how to fix it.");
}

// use the mop to fix the server
function hServer_UseInv()
{
  player.Walk(hServer.WalkToX, hServer.WalkToY, eBlock);
  player.FaceDirection(eDirectionUp);
  if (player.ActiveInventory == iMop)
  {
    player.Say("Here we go.");
    player.Say("Oh boy, I think I have to close my eyes.");
    Wait(20);
    gBlack.Visible = true;
    Wait(60);
    player.Say("BANG! BANG! BANG!");
    Wait(60);
    oButtons.Visible = true;
    gBlack.Visible = false;
    Wait(60);
    player.Say("Nice, the buttons turned green. I think it works now.");
    bForce = true;
  }
  else
  {
    player.Say("That won't do anything.");
  }
  player.ActiveInventory = null;
}
