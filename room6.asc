// room script file

// Interact with door
function hDoor_Interact()
{
  player.Say("No.");
}

// Load room and setup characters
function room_Load()
{
  cRoger.Transparency = 100;
  cRoger.Clickable = false;
  cWhite.ChangeRoom(6, 224, 136);
  cWhite.SetAsPlayer();
}

// Switch music and setup characters
function room_FirstLoad()
{
  aMusic.Stop();
  aMusic = aLose.Play(eAudioPriorityHigh, eRepeat);
  FadeOut(64);
  cWhite.Transparency = 100;
  gBlack.Visible = false;
  FadeIn(1);
  cWhite.Say("BRKOKLOSKR..brrrr");
  int trans = cWhite.Transparency;
  while (trans > 0) {
    trans--;
    cWhite.Transparency = trans;
    Wait(1);
  }
}

// Interact with character in bed, start dialog
function hEgo_Interact()
{
  cWhite.Walk(66, 131, eBlock);
  cWhite.FaceDirection(eDirectionDown);
  cWhite.Say("Prkvlowsktk.");
  cRoger.SayAt(30, 110, 100, "How did you find me?");
  Wait(60);
  cRoger.SayAt(30, 110, 100, "Forget about it.");
  cRoger.Say("...");
  cRoger.SayAt(30, 110, 100, "I knew this would happen.");
  dEnd.Start();
}

// Pick up remote control from TV
function hTV_AnyClick()
{
  if (cWhite.HasInventory(iRemote))
  {
    cWhite.Say("No.");
  }
  else
  {
    cWhite.Walk(hTV.WalkToX, hTV.WalkToY, eBlock);
    oTV.SetView(11);
    oTV.Visible = true;
    aNoise.Play(eAudioPriorityNormal, eOnce);
    oTV.Animate(0, 5, eOnce, eBlock);
    oTV.Visible = false;
    cWhite.Say("Bzz. Control!");
    cWhite.AddInventory(iRemote);
  }
}

// Use remote control on Bert
function hEgo_UseInv()
{
  if (cWhite.ActiveInventory == iRemote)
  {
    cWhite.Walk(hEgo.WalkToX, hEgo.WalkToY, eBlock);
    cWhite.Say("O9JKH65TR.");
    Wait(20);
    bEndAct = true;
    FadeOut(1);
    cWhite.ChangeRoom(3, 160, 100);
    cRoger.ChangeRoom(3, 160, 100);
    cWhite.Transparency = 100;
    gBlack.Visible = true;
  }
}

function hDoor_UseInv()
{
  cWhite.Say("KRLYIHTTSHE!!!");
}
