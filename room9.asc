// room script file

bool bGuy = false;

// If Roy is gone, pick up the journal form the board and leave
function hBoard_AnyClick()
{
  if (cFiona.ActiveInventory == null)
  {
    cFiona.Walk(hBoard.WalkToX, hBoard.WalkToY, eBlock);
    if (bGuy) 
    {
      cFiona.FaceDirection(eDirectionUp);
      Wait(20);
      cFiona.FaceDirection(eDirectionDown);
      cFiona.Say("There's a journal in there.");
      gBook.Visible = true;
      cFiona.Say("Interesting. It's a psychological evaluation of a group of people.");
      cFiona.Say("Or not quite? It is only refered to as 'the subjects'.");
      cFiona.Say("The later entries become increasingly worried.");
      cFiona.Say("I read things like 'Most of them cannot be reasoned with.' and 'We should abort the test.'");
      gBook.Visible = false;
      cFiona.Say("I'll take it with me and send a copy to the institute.");
      cFiona.AddInventory(iBook);
      cFiona.Say("I'll head to the library. Maybe they have a scanner.");
      cFiona.Walk(267, 144, eBlock);
      cFiona.Walk(317, 140, eBlock, eAnywhere);
      // FadeOut-trick
      FadeOut(1);
      gBlack.Visible = true;
      cFiona.ChangeRoom(10, 133, 235, eDirectionUp); 
    }
    else
    {
      cGuy.Say("Hey! You may 'look', not 'touch'.");
      cGuy.Say("I cannot afford to have this room cleaned all the time!");
    }
  }
  else
  {
    cFiona.Say("No need to do that.");
  }
}

// Play a different music, init room
function room_Load()
{
  aMusic.Stop();
  aMusic = aInside.Play(eAudioPriorityHigh, eRepeat);
  oStrings.SetView(10);
  oStrings.Animate(0, 10, eRepeat, eNoBlock);
  cGuy.ChangeRoom(9, 181, 132, eDirectionDown);
}

// Look at the bed
function hBed_Look()
{
  if (oGoo.Visible)
  {
    cFiona.FaceLocation(76, 132, eBlock);
    cFiona.Say("It really looks like a mess.");
  }
  else
  {
    cFiona.FaceLocation(76, 132, eBlock);
    cFiona.Say("A bed. Maybe I can check if he left something?");
  }
}

// Interact with the bed
function hBed_Interact()
{
  cFiona.Walk(hBed.WalkToX, hBed.WalkToY, eBlock);
  cFiona.FaceLocation(112, 144, eBlock);
  cFiona.Say("Hm. Not sure what to do with it.");
}

// Reveal residue when the black light is used on the bed
function hBed_UseInv()
{
  cFiona.Walk(hBed.WalkToX, hBed.WalkToY, eBlock);
  cFiona.FaceLocation(112, 144, eBlock);
  if ((cFiona.ActiveInventory == iBlacklight) && (bGuy == false))
  {
    Wait(20);
    cFiona.Say("Hey, I can see a trace of something.");
    Wait(40);
    oGoo.Transparency = 100;
    oGoo.Visible = true;
    int trans = oGoo.Transparency;
    while (trans > 0) {
      trans--;
      oGoo.Transparency = trans;
      Wait(1);
    }
    cFiona.Say("Ewww. What a mess.");
    cFiona.ActiveInventory = null;
    Wait(40);
    cGuy.Say("What the heck!");
    cGuy.Say("That is unaccaptable.");
    cGuy.Say("And those guys call themselves dry cleaners?");
    cGuy.Say("I'm going to call those jokers right away.");
    cGuy.Walk(267, 144, eBlock, eAnywhere);
    Wait(20);
    oDoorOpen.Visible = true;
    cGuy.Walk(317, 140, eBlock, eAnywhere);
    bGuy = true;
    cFiona.Say("Phew... how can motels like this even exist?");
  }
  else
  {
    if (cFiona.ActiveInventory == iBlacklight)
    {
      cFiona.Say("I already did that.");
    }
    else
    {
      cFiona.Say("No, that won't work.");
    }
  }
}

// Init monolog of Roy after fadein
function room_AfterFadeIn()
{
  cGuy.Say("Can you believe it? I still had the keys in my pocket!");
  cGuy.Say("Anyway, hurry, have a look around!");
}

// Little monologe when interacting with the TV
function hTV_Interact()
{
  cFiona.Walk(hTV.WalkToX, hTV.WalkToY, eBlock, eWalkableAreas);
  cFiona.FaceDirection(eDirectionUp);
  if (!bGuy)
  {
    cGuy.Say("Out of operation.");
    cGuy.Say("Someone stole the remote control.");
  }
  else
  {
    cFiona.Say("It can't be switched on without a remote control.");
  }
}

// Look at the TV
function hTV_Look()
{
  cFiona.FaceLocation(hTV.WalkToX, hTV.WalkToY-30, eBlock);
  cFiona.Say("A TV.");
}
